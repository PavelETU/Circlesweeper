plugins {
    id 'com.android.application'
    id 'com.dicedmelon.gradle.jacoco-android'
    id 'com.github.triplet.play' version '2.0.0'
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.wordpress.lonelytripblog.circlesminesweeper"
        minSdkVersion 19
        targetSdkVersion 32
        versionCode 6
        versionName "1.5"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Thanks to Valentin Hinov's article
    // "Using TravisCI to securely build and deploy a signed version of your Android app."
    def isRunningOnTravis = System.getenv("CI") == "true"
    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.release.storeFile = file("../android_circlesweeper.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }
}

play {
    track = 'production'
    serviceAccountEmail = "continuousdeliveryservice@api-7869286387186355571-549281.iam.gserviceaccount.com"
    serviceAccountCredentials = file "../api-7869286387186355571-549281-6a9bc1bd0399.p12"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.7.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0-alpha01'

    // Architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.room:room-runtime:2.4.2'
    annotationProcessor 'androidx.room:room-compiler:2.4.2'

    // Dagger 2
    implementation 'com.google.dagger:dagger-android:2.35.1'
    implementation 'com.google.dagger:dagger-android-support:2.35.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.35.1'
    implementation 'com.google.dagger:dagger:2.40.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.40.1'

    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.mockito:mockito-core:2.23.4"
}